{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-26.05";
    sops-nix = {
      url = "github:Mic92/sops-nix";
      inputs = {
        nixpkgs.follows = "nixpkgs";
      };
    };
    flake-utils.url = "github:numtide/flake-utils";
    pedantix = {
      url = "github:Swarsel/pedantix";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        treefmt-nix.follows = "treefmt-nix";
      };
    };
    garnix-guest-lib = {
      url = "github:OSSystems/garnix-guest-lib";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        pedantix.follows = "pedantix";
        treefmt-nix.follows = "treefmt-nix";
      };
    };
    treetop = {
      url = "github:soenkehahn/treetop";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        flake-utils.follows = "flake-utils";
        crane.follows = "crane";
      };
    };
    crane = {
      url = "github:ipetkov/crane";
    };
    comment = {
      url = "github:garnix-io/comment";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        flake-utils.follows = "flake-utils";
        crane.follows = "crane";
      };
    };
    cradle = {
      url = "github:garnix-io/cradle";
      inputs = {
        nixpkgs.follows = "nixpkgs";
        flake-utils.follows = "flake-utils";
      };
    };
    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };
  outputs =
    flakeInputs@{
      self,
      cradle,
      flake-utils,
      nixpkgs,
      sops-nix,
      treefmt-nix,
      ...
    }:
    let
      overlays = [
        (outerFinal: outerPrev: {
          haskellPackages =
            with outerPrev.haskell.lib;
            outerPrev.haskellPackages.override {
              overrides = final: prev: {
                hashids = doJailbreak (
                  prev.hashids.overrideAttrs (old: {
                    meta = old.meta // {
                      broken = false;
                    };
                  })
                );
                generic-random = prev.callPackage ./nix/packages/generic-random.nix { };
                HDBC = prev.callPackage ./nix/packages/HDBC.nix { };
                servant-github-webhook = prev.callPackage ./nix/packages/servant-github-webhook.nix { };
                generics-eot = dontCheck (prev.callPackage ./nix/packages/generics-eot.nix { });
                iso-deriving = prev.callPackage ./nix/packages/iso-deriving.nix { };
                github-app = prev.callPackage ./nix/packages/github-app.nix { };
                github-webhooks = prev.callPackage ./nix/packages/github-webhooks.nix { };
                oauth2-simple = prev.callPackage ./nix/packages/oauth2-simple.nix { };
                cradle = cradle.lib.${outerPrev.stdenv.hostPlatform.system}.mkCradle final;
                garnix =
                  (dontHaddock (
                    disableLibraryProfiling (
                      disableExecutableProfiling (final.callPackage ./nix/packages/garnix.nix { })
                    )
                  )).overrideAttrs
                    (old: { });
              };
            };
        })
        (final: prev: {
          opensearch-dashboards = final.callPackage ./nix/packages/opensearch-dashboards/default.nix { };

          # See https://github.com/NixOS/nixpkgs/issues/319323
          opensearch = prev.opensearch.overrideAttrs (old: {
            # Workaround for packaging bug (deleting opensearch-cli breaks
            # opensearch-plugin/opensearch-keystore command)
            installPhase = builtins.replaceStrings [ "rm $out/bin/opensearch-cli\n" ] [ "" ] old.installPhase;
          });
        })
      ];
    in
    flake-utils.lib.eachDefaultSystem (
      system:
      let
        pkgs = import nixpkgs { inherit overlays system; };
        lib = nixpkgs.lib;
        namespace =
          prefix: attrSet:
          lib.mapAttrs' (name: value: {
            name = "${prefix}_${name}";
            inherit value;
          }) attrSet;
        subDirInputs = {
          inherit
            flakeInputs
            pkgs
            self
            system
            ;
          lib = nixpkgs.lib;
        };

        treefmt = import ./nix/treefmt.nix subDirInputs;
        backend = import ./backend subDirInputs;
        frontend = import ./frontend subDirInputs;
        frontend-age-wasm = import ./frontend/age-wasm subDirInputs;
        provisioner = import ./provisioner subDirInputs;
        hosting-gateway = import ./hosting-gateway subDirInputs;
      in
      {
        apps = lib.mapAttrs (_: drv: {
          type = "app";
          program = lib.getExe drv;
          meta.description = drv.meta.description;
        }) (namespace "backend" backend.commands);

        checks =
          namespace "backend" backend.checks
          // namespace "frontend" frontend.checks
          // namespace "frontend" (namespace "ageWasm" frontend-age-wasm.checks)
          // namespace "provisioner" provisioner.checks
          // namespace "hostingGateway" hosting-gateway.checks
          //
            # NixOS VM tests only build on Linux.
            lib.optionalAttrs pkgs.stdenv.isLinux {
              nixosTests_hostingDeploy = import ./nix/tests/hosting-deploy.nix subDirInputs;
            };

        packages =
          lib.mapAttrs' (name: value: {
            name = "hosting-gateway/${name}";
            inherit value;
          }) hosting-gateway.packages
          // namespace "backend" backend.packages
          // namespace "frontend" frontend.packages
          // namespace "frontend" (namespace "ageWasm" frontend-age-wasm.packages);

        formatter = treefmt.wrapper;

        devShells.default = pkgs.mkShell {
          shellHook = backend.shellHook;
          buildInputs = [
            pkgs.just
            pkgs.nil
            pkgs.nix
            (pkgs.callPackage ./nix/packages/withSecrets.nix { })
          ]
          ++ backend.devShellInputs
          ++ frontend.devShellInputs
          ++ hosting-gateway.devShellInputs;
        };
      }
    )
    // {
      nixosModules = {
        garnix = ./nix/modules/garnix-server.nix;
        default = ./nix/modules/garnix-server.nix;
        garnix-provisioner = {
          imports = [ ./nix/modules/microvm-provisioner.nix ];
          garnix.local-provisioner.guestProfile = nixpkgs.lib.mkDefault "${flakeInputs.garnix-guest-lib}/guest-profile.nix";
        };
        garnix-hosting-gateway = ./hosting-gateway/nixos-module.nix;
        garnix-guest = flakeInputs.garnix-guest-lib.nixosModules.garnix-guest;
      };
      nixosConfigurations =
        (import ./nix/website.nix {
          inherit flakeInputs overlays self;
        }).nixosConfigurations
        // (import ./examples/example-selfhost.nix {
          inherit flakeInputs overlays self;
        }).nixosConfigurations;
    };
}
