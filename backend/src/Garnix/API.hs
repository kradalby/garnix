module Garnix.API where

import Autodocodec.Schema (JSONSchema)
import Data.Text qualified as T
import Garnix.API.Account
import Garnix.API.Auth
import Garnix.API.Badges
import Garnix.API.Builds
import Garnix.API.Cache
import Garnix.API.Commits
import Garnix.API.ConfigSchema (garnixConfigJsonSchema)
import Garnix.API.Dev (DevAPI, devAPI)
import Garnix.API.GhWebhooks
import Garnix.API.GiteaWebhooks
import Garnix.API.Health
import Garnix.API.Hosts
import Garnix.API.Keys
import Garnix.API.Modules
import Garnix.API.Runs (RunAPI, runAPI)
import Garnix.API.Stripe (StripeWebhookAPI, stripeWebhookAPI)
import Garnix.DB qualified as DB
import Garnix.Forge.Types (githubAppConfigName)
import Garnix.Monad
import Garnix.Prelude
import Garnix.Types
import Servant
import Servant.Auth.Server

type CT = '[JSON]

api :: Proxy (ToServant WholeAPI AsApi)
api = genericApi (Proxy :: Proxy WholeAPI)

data WholeAPI r = WholeAPI
  { events ::
      r
        :- "api"
          :> "events"
          :> ( "github" :> ToServantApi GhWebhookAPI
                 :<|> "stripe" :> StripeWebhookAPI
                 :<|> "gitea" :> ToServantApi GiteaWebhookAPI
             ),
    account :: r :- "api" :> "account" :> Auth '[JWT, Cookie] AuthJwtPayload :> ToServantApi AccountAPI,
    build :: r :- "api" :> "build" :> Auth '[JWT, Cookie] AuthJwtPayload :> ToServantApi BuildAPI,
    commit :: r :- "api" :> "commits" :> Auth '[JWT, Cookie] AuthJwtPayload :> ToServantApi CommitAPI,
    run :: r :- "api" :> "run" :> Auth '[JWT, Cookie] AuthJwtPayload :> ToServantApi RunAPI,
    modules :: r :- "api" :> "modules" :> Auth '[JWT, Cookie] AuthJwtPayload :> ToServantApi ModulesAPI,
    dev :: r :- "api" :> "dev" :> ToServantApi DevAPI,
    hosts :: r :- "api" :> "hosts" :> ToServantApi HostsAPI,
    keys :: r :- "api" :> "keys" :> Capture "owner" RepoOwner :> Capture "repo" RepoName :> "repo-key.public" :> Get '[PlainText] PublicKey,
    actionKeys :: r :- "api" :> "keys" :> Capture "owner" RepoOwner :> Capture "repo" RepoName :> "actions" :> Capture "action" PackageName :> "key.public" :> Get '[PlainText] PublicKey,
    login :: r :- "api" :> "login" :> ToServantApi LoginAPI,
    signup :: r :- "api" :> "signup" :> ToServantApi SignupAPI,
    whoami :: r :- "api" :> "whoami" :> Auth '[JWT, Cookie] AuthJwtPayload :> Get '[JSON] (Maybe UserDto),
    authJwt :: r :- "api" :> "auth" :> "jwt" :> ToServantApi AuthJwtAPI,
    config :: r :- "api" :> "config" :> Get '[JSON] FrontendConfig,
    badges :: r :- "api" :> "badges" :> Capture "owner" RepoOwner :> Capture "repo" RepoName :> QueryParam "branch" Branch :> Get '[JSON] Badge,
    waitlist :: r :- "api" :> "waitlist" :> ReqBody '[JSON] Email :> Post '[JSON] (),
    cache :: r :- "api" :> "cache" :> ToServantApi CacheAPI,
    garnixConfigSchema :: r :- "api" :> "garnix-config-schema.json" :> Get '[JSON] JSONSchema,
    health :: r :- "api" :> "health" :> ToServantApi HealthAPI
  }
  deriving stock (Generic)

data ProjectAPI r = ProjectAPI
  { get ::
      r
        :- Capture "gh_owner" Text
          :> Capture "gh_repo" Text
          :> "commit"
          :> Capture "commit" CommitHash
          :> Get CT (),
    post ::
      r
        :- Capture "gh_owner" Text
          :> Capture "gh_repo" Text
          :> "commit"
          :> Capture "commit" CommitHash
          :> QueryParam "token" ForgeToken
          :> Post '[JSON] RunResult
  }
  deriving stock (Generic)

wholeAPI :: WholeAPI (AsServerT M)
wholeAPI =
  WholeAPI
    { events = toServant ghWebhookAPI :<|> stripeWebhookAPI :<|> toServant giteaWebhookAPI,
      account = toServant . accountAPI,
      dev = devAPI,
      login = toServant loginAPI,
      signup = toServant signupAPI,
      whoami = whoAmIAPI,
      authJwt = toServant authJwtAPI,
      hosts = toServant hostsAPI,
      keys = Garnix.API.Keys.getRepoPublicKey,
      actionKeys = Garnix.API.Keys.getActionPublicKey,
      config = getConfig,
      build = toServant . buildAPI,
      commit = toServant . commitAPI,
      run = toServant . runAPI,
      modules = toServant . modulesAPI,
      badges = badgesAPI,
      waitlist = waitlistAPI,
      cache = toServant cacheAPI,
      garnixConfigSchema = pure garnixConfigJsonSchema,
      health = toServant healthAPI
    }

getConfig :: M FrontendConfig
getConfig = do
  ghAppName <- githubAppConfigName <$> githubAppConfig
  pure $ FrontendConfig {_frontendConfigGithubAppName = ghAppName}

waitlistAPI :: Email -> M ()
waitlistAPI email = do
  let isValid = '@' `T.elem` getEmail email
  unless isValid $ throw InvalidEmail
  DB.addToWaitlist email
